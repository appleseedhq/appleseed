
//
// This source file is part of appleseed.
// Visit http://appleseedhq.net/ for additional information and resources.
//
// This software is released under the MIT license.
//
// Copyright (c) 2017 Luis Barrancos, The appleseedhq Organization
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

// Ref: $MAYA_LOCATION/docs/Nodes/bump2d.html

#include "appleseed/maya/as_maya_helpers.h"

shader as_maya_bump2d
[[
    string as_maya_node_name = "bump2d"
]]
(
    float in_bumpValue = 0.0
    [[
        string as_maya_attribute_name = "bumpValue",
        string label = "Bump Value",
        string page = "2D Bump Attributes"
    ]],
    float in_bumpDepth = 1.0
    [[
        string as_maya_attribute_name = "bumpDepth",
        string label = "Bump Depth",
        string page = "2D Bump Attributes"
    ]],
    int in_bumpInterp = 0
    [[
        string as_maya_attribute_name = "bumpInterp",
        string label = "Bump Interpolation Mode",
        string widget = "mapper",
        string options = "Bump:0|Tangent Space Normals:1|Object Space Normals:2",
        string page = "2D Bump Attributes"
    ]],
    float in_bumpFilter = 1.0
    [[
        string as_maya_attribute_name = "bumpFilter",
        string label = "Bump Filter",
        string page = "Effects"
    ]],
    float in_bumpFilterOffset = 0.0
    [[
        string as_maya_attribute_name = "bumpFilterOffset",
        string label = "Bump Filter Offset",
        string page = "Effects"
    ]],
    int in_adjustEdges = 0
    [[
        string as_maya_attribute_name = "adjustEdges",
        string label = "Adjust Edges",
        string page = "Effects",
        string widget = "checkBox"
    ]],
    color in_normalMap = color(0)
    [[
        string as_maya_attribute_name = "asNormalMap",
        string page = "Appleseed",
        string widget = "null"
    ]],
    int in_normalMapMode = 0
    [[
        string as_maya_attribute_name = "asNormalMapMode",
        string widget = "mapper",
        string options = "unsigned:0|signed:1",
        string page = "Appleseed",
    ]],
    int in_normalMapFlipR = 0
    [[
        string as_maya_attribute_name = "asNormalMapFlipR",
        string widget = "checkBox",
        string page = "Appleseed"
    ]],
    int in_normalMapFlipG = 0
    [[
        string as_maya_attribute_name = "asNormalMapFlipG",
        string widget = "checkBox",
        string page = "Appleseed"
    ]],
    int in_normalMapSwapRG = 0
    [[
        string as_maya_attribute_name = "asNormalMapSwapRG",
        string widget = "checkBox",
        string page = "Appleseed"
    ]],
    point in_pointCamera = P
    [[
        string as_maya_attribute_name = "pointCamera",
        string label = "Surface Point",
        string widget = "null"
    ]],
    normal in_normalCamera = N
    [[
        string as_maya_attribute_name = "normalCamera",
        string label = "Surface Normal",
        string widget = "null"
    ]],
    vector Tn = vector(0)
    [[
        int lockgeom = 0,
        string widget = "null"
    ]],
    vector Bn = vector(0)
    [[
        int lockgeom = 0,
        string widget = "null"
    ]],

    MAYA_UV_PARAMETERS,

    output normal out_outNormal = normal(0.0, 0.0, 1.0)
    [[
        string as_maya_attribute_name = "outNormal",
        string label = "Output Normal",
        string widget = "null"
    ]]
)
{
    if (in_bumpValue * in_bumpDepth == 0.0)
    {
        out_outNormal = normalize(in_normalCamera);
    }
    else
    {
        float bump_depth = abs(in_bumpDepth);

        // Halve the bump value to avoid excess clamp.
        float bump_offset = clamp(
            in_bumpValue * 0.5 * in_bumpDepth, -bump_depth, bump_depth);

        normal surface_normal = normalize(in_normalCamera);

        if (in_bumpInterp == 0)
        {
            bump_offset /= length(transform("object", surface_normal));

            out_outNormal = normalize(calculatenormal(
                in_pointCamera + (vector) surface_normal * bump_offset));
        }
        else if (in_bumpInterp == 1)
        {
            normal lookup_normal = (in_normalMapMode == 0)
                ? (normal) clamp(in_normalMap, 0.0, 1.0) * 2.0 - 1.0
                : (normal) clamp(in_normalMap, -1.0, 1.0);

            if (lookup_normal == 0.0)
            {
                out_outNormal = surface_normal;
            }
            else
            {
                if (in_normalMapFlipR)
                {
                    lookup_normal[0] = -lookup_normal[0];
                }
                if (in_normalMapFlipG)
                {
                    lookup_normal[1] = -lookup_normal[1];
                }
                if (in_normalMapSwapRG)
                {
                    float lookup_tmp = lookup_normal[0];
                    lookup_normal[0] = lookup_normal[1];
                    lookup_normal[1] = lookup_tmp;
                }

                vector bitangent = cross(Tn, surface_normal);

                normal reoriented_normal =
                    lookup_normal[0] * Tn +
                    lookup_normal[1] * bitangent +
                    lookup_normal[2] * surface_normal;

                out_outNormal = mix(
                    surface_normal,
                    reoriented_normal,
                    bump_offset);

                out_outNormal = normalize(out_outNormal);
            }
        }
        else if (in_bumpInterp == 2)
        {
            normal lookup_normal = (in_normalMapMode == 0)
                ? (normal) in_normalMap * 2.0 - 1.0
                : (normal) in_normalMap;

            if (lookup_normal == 0.0)
            {
                out_outNormal = surface_normal;
            }
            else
            {
                normal xformed_normal =
                    transform("object", "common", lookup_normal);

                out_outNormal = mix(
                    surface_normal,
                    xformed_normal,
                    bump_offset);

                out_outNormal = normalize(out_outNormal);
            }
        }
        else
        {
#ifdef DEBUG
            warning("[WARNING]: Bump mode invalid in %s:%i\n",
                    __FILE__, __LINE__);
#endif
        }
    }
}
