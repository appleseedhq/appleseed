
//
// This source file is part of appleseed.
// Visit http://appleseedhq.net/ for additional information and resources.
//
// This software is released under the MIT license.
//
// Copyright (c) 2016 Luis Barrancos, The appleseedhq Organization
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

// Ref: $MAYA_LOCATION/docs/Nodes/grid.html

#include "appleseed/maya/as_maya_helpers.h"
#include "appleseed/pattern/as_pattern_helpers.h"

shader as_maya_grid
[[
    string maya_node_name = "grid",
    string maya_classification = "texture/2d"
]]
(
    color in_fillerColor = color(0)
    [[
        string maya_attribute_name = "fillerColor",
        string maya_attribute_type = "vector",
        string label = "Filler Color"
    ]],
    color in_lineColor = color(0)
    [[
        string maya_attribute_name = "lineColor",
        string maya_attribute_type = "vector",
        string label = "Line Color"
    ]],
    float in_contrast = 1.0
    [[
        string maya_attribute_name = "contrast",
        string maya_attribute_type = "float",
        string label = "Contrast"
    ]],
    float in_uWidth = 0.1
    [[
        string maya_attribute_name = "uWidth",
        string maya_attribute_type = "float",
        string label = "U Width"
    ]],
    float in_vWidth = 0.1
    [[
        string maya_attribute_name = "vWidth",
        string maya_attribute_type = "float",
        string label = "V Width"
    ]],

    MAYA_COLORBALANCE_PARAMETERS,
    MAYA_EFFECTS_PARAMETERS,
    MAYA_UV_PARAMETERS,

    output color out_outColor = color(0)
    [[
        string maya_attribute_name = "outColor",
        string maya_attribute_type = "vector",
        string label = "Output Color",
        string widget = "null"
    ]],
    output float out_outAlpha = 0.0
    [[
        string maya_attribute_name = "outAlpha",
        string maya_attribute_type = "float",
        string label = "Output Alpha",
        string widget = "null"
    ]]
)
{
    if (in_uvCoord[0] == OUTSIDE_UVFRAME || in_uvCoord[1] == OUTSIDE_UVFRAME)
    {
        out_outColor = in_defaultColor;
        out_outAlpha = maya_luminance(in_defaultColor);
    }
    else
    {
        float dsdt[2] = {0, 0};
        float st[2] = {in_uvCoord[0], 1 - in_uvCoord[1]};
        float width[2] = {in_uWidth, in_vWidth};

        st[0] = mod(st[0], 1);
        st[1] = mod(st[1], 1);
        
        if (in_filter)
        {
            // Use uvFilterSize (unused atm) or compute st filter widths.
            dsdt[0] = isconnected(in_uvFilterSize[0])
                ? in_uvFilterSize[0]
                : filterwidth(in_uvCoord[0]);

            dsdt[1] = isconnected(in_uvFilterSize[1])
                ? in_uvFilterSize[1]
                : filterwidth(in_uvCoord[1]);

            dsdt[0] *= in_filter;
            dsdt[1] *= in_filter;
        }

        dsdt[0] += in_filterOffset;
        dsdt[1] += in_filterOffset;

        // Contrast default 1, with 0 being full blend of input colors+alpha.
        color color_blend = mix(in_fillerColor, in_lineColor, 0.5);
        float alpha_blend = 0.5;

        color A = mix(color_blend, in_fillerColor, in_contrast);
        color B = mix(color_blend, in_lineColor, in_contrast);

        float weight =
            filtered_pulsetrain(width[0], 1, st[0] + width[0] * .5, dsdt[0]) *
            filtered_pulsetrain(width[1], 1, st[1] + width[1] * .5, dsdt[1]);

        out_outColor = mix(A, B, weight);
        out_outAlpha = mix(alpha_blend, 1 - weight, in_contrast);

        maya_colorBalance(
            in_colorGain,
            in_colorOffset,
            in_defaultColor,
            in_alphaGain,
            in_alphaOffset,
            in_invert,
            in_alphaIsLuminance,
            out_outColor,
            out_outAlpha);
    }
}

