
//
// This source file is part of appleseed.
// Visit http://appleseedhq.net/ for additional information and resources.
//
// This software is released under the MIT license.
//
// Copyright (c) 2016 Luis Barrancos, The appleseedhq Organization
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
// THE SOFTWARE.
//

// Ref: $MAYA_LOCATION/docs/Nodes/phong.html

shader as_maya_phong
[[
    string as_maya_node_name = "phong"
]]
(
    color in_color = color(0.5)
    [[
        string as_maya_attribute_name = "color",
        string label = "Color",
        string page = "Common Material Attributes"
    ]],
    color in_transparency = color(0)
    [[
        string as_maya_attribute_name = "transparency",
        string label = "Transparency",
        string page = "Common Material Attributes"        
    ]],
    color in_ambientColor = color(0)
    [[
        string as_maya_attribute_name = "ambientColor",
        string label = "Ambient Color",
        string page = "Common Material Attributes"
    ]],
    color in_incandescence = color(0)
    [[
        string as_maya_attribute_name = "incandescence",
        string label = "Incandescence Color",
        string page = "Common Material Attributes"
    ]],
    normal in_normalCamera = N
    [[
        string as_maya_attribute_name = "normalCamera",
        string label = "Bump Mapping Normal",
        string page = "Common Material Attributes"
    ]],
    float in_diffuse = 0.8
    [[
        string as_maya_attribute_name = "diffuse",
        string label = "Diffuse Intensity",
        string page = "Common Material Attributes"
    ]],
    float in_translucence = 0.0
    [[
        string as_maya_attribute_name = "translucence",
        string label = "Translucence",
        string page = "Common Material Attributes"
    ]],
    float in_translucenceDepth = 0.5
    [[
        string as_maya_attribute_name = "translucenceDepth",
        string label = "Translucence Depth",
        string page = "Common Material Attributes"
    ]],  
    float in_translucenceFocus = 0.5
    [[
        string as_maya_attribute_name = "translucenceFocus",
        string label = "Translucence Focus",
        string page = "Common Material Attributes"
    ]],
    float in_cosinePower = 20.0
    [[
        string as_maya_attribute_name = "cosinePower",
        string label = "Cosine Power",
        float min = 1.0,
        float max = 100000.0,
        float softmin = 2.0,
        float softmax = 100.0,
        string page = "Specular Shading"
    ]],
    color in_specularColor = color(0.5)
    [[
        string as_maya_attribute_name = "specularColor",
        string label = "Specular Color",
        string page = "Specular Shading"
    ]],
    float in_reflectivity = 0.5
    [[
        string as_maya_attribute_name = "reflectivity",
        string label = "Reflectivity",
        string page = "Specular Shading"
    ]],
    color in_reflectedColor = color(0)
    [[
        string as_maya_attribute_name = "reflectedColor",
        string label = "Reflected Color",
        string page = "Specular Shading"
    ]],
    int in_matteOpacityMode = 2
    [[
        string as_maya_attribute_name = "matteOpacityMode",
        string label = "Matte Opacity Mode",
        string widget = "mapper",
        string options = "Black Hole:0|Solid Matte:1|Opacity Gain:2",
        string page = "Matte Opacity"
    ]],
    float in_matteOpacity = 1.0
    [[
        string as_maya_attribute_name = "matteOpacity",
        string label = "Matte Opacity",
        string page = "Matte Opacity"
    ]],
    int in_refractions = 0
    [[
        string as_maya_attribute_name = "refractions",
        string label = "Refractions",
        string widget = "checkBox",
        string page = "Raytrace Options"
    ]],
    float in_refractiveIndex = 1.0
    [[
        string as_maya_attribute_name = "refractiveIndex",
        string label = "Refractive Index",
        float min = 0.010,
        float max = 3.0,
        float softmin = 0.010,
        float softmax = 3.0,
        string page = "Raytrace Options"
    ]],
    int in_refractionLimit = 1
    [[
        string as_maya_attribute_name = "refractionLimit",
        string label = "Refraction Limit",
        string widget = "intslider",
        int min = 0,
        int max = 10,
        int softmin = 0,
        int softmax = 10,
        string page = "Raytrace Options"
    ]],
    float in_shadowAttenuation = 0.5
    [[
        string as_maya_attribute_name = "shadowAttenuation",
        string label = "Shadow Attenuation",
        string page = "Raytrace Options"
    ]],

    int in_hideSource = 0
    [[
        string as_maya_attribute_name = "hideSource",
        string label = "Hide Glow Source",
        string widget = "checkBox",
        string page = "Special Effects"
    ]],
    float in_glowIntensity = 0.0
    [[
        string as_maya_attribute_name = "glowIntensity",
        string label = "Glow Intensity",
        string page = "Special Effects"
    ]],

    output closure color out_outColor = 0
    [[
        string as_maya_attribute_name = "outColor",
        string as_maya_attribute_short_name = "oc",
        string label = "Output Color",
        string widget = "null"
    ]],
    output closure color out_outTransparency = 0
    [[
        string as_maya_attribute_name = "outTransparency",
        string as_maya_attribute_short_name = "ot",
        string label = "Output Transparency",
        string widget = "null"
    ]],
    output closure color out_outGlowColor = 0
    [[
        string as_maya_attribute_name = "outGlowColor",
        string as_maya_attribute_short_name = "og",
        string label = "Output Glow Color",
        string widget = "null"
    ]],
    output closure color out_outMatteOpacity = 0
    [[
        string as_maya_attribute_name = "outMatteOpacity",
        string as_maya_attribute_short_name = "om",
        string label = "Output Matte Opacity",
        string widget = "null"
    ]]
)
{   
    if (raytype("shadow"))
    {
        color transparency = in_transparency ;

        // Shadow attenuation: 0=constant, 1=fake caustics.
        if (in_refractions && in_transparency)
        {
            transparency *= mix(
                transparency,
                abs(dot(normalize(I), normalize(in_normalCamera))),
                in_shadowAttenuation
                );
        }
        out_outColor = transparency * transparent();
        out_outTransparency = out_outColor;
    }
    else
    {
        normal Nn = normalize(in_normalCamera);

        closure color transparency_term = 0, incandescence_term = 0;
        closure color translucency_term = 0, diffuse_term = 0;
        closure color specular_term = 0, refraction_term = 0;
        closure color glow_term = 0;

        color opacity = 1.0 - in_transparency;

        if (in_color)
        {
            diffuse_term = opacity * in_diffuse * in_color * diffuse(Nn);
            
            translucency_term = opacity * in_translucence *
                in_color * translucent(Nn);
        }

        if (raytype("light") && in_incandescence)
        {
            incandescence_term = opacity * in_incandescence * emission();
        }

        // Maya associates the transparency with the BTDF.
        if (in_refractions)
        {
            int ray_depth = 0;
            int status = getattribute("path:ray_depth", ray_depth);

            if (status && ray_depth <= in_refractionLimit)
            {
                // Maya associates transparency with the BTDF.
                refraction_term = in_transparency *
                    refraction(Nn, in_refractiveIndex);
            }

            opacity = 1.0;
        }
        else
        {
            transparency_term = in_transparency * transparent();
        }

        if (in_specularColor)
        {
            specular_term = opacity * in_specularColor *
                phong(Nn, in_cosinePower * 10);
        }

        closure color matte_term;

        if (in_matteOpacityMode == 0)
        {
            matte_term = 0;
        }
        else if (in_matteOpacityMode == 1)
        {
            matte_term = holdout();
        }
        else
        {
            matte_term = in_matteOpacity * holdout();
        }
        
        if (in_glowIntensity)
        {
            // glow_term = opacity * in_glowIntensity * post_glow();
            // out_outGlowColor = glow_term;
        }

        out_outColor =
            incandescence_term +
            diffuse_term +
            translucency_term +
            specular_term +
            refraction_term +
            matte_term +
            glow_term +
            transparency_term;

        out_outTransparency = transparency_term;
        out_outMatteOpacity = matte_term;
        out_outGlowColor = glow_term;
    }
}

